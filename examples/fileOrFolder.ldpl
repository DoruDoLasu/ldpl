# File or Folder Example
# written in LDPL by Martín del Río
# https://www.ldpl-lang.org/
# Created for LDPL 3.0.6 'Diligent Dreadnoughtus'

# --- Description -------------------------------------
# Lists all the files in the current directory. Then,
# for every file, it displays if the file is a file or
# a directory.
# -----------------------------------------------------

DATA:
filesInDirectory is text list
filesInDirectory2 is text list
numberOfFiles is number
i is number
fileContents is text

PROCEDURE:
list contents of directory "." in filesInDirectory
if errorCode is equal to 0 then
    store length of filesInDirectory in numberOfFiles
    display "Found " numberOfFiles " file(s)." crlf
    # List all the files
    while i is less than numberOfFiles do
        display "> " filesInDirectory:i
        list contents of directory filesInDirectory:i in filesInDirectory2
        if errorCode is equal to 0 then
            display " (directory)" crlf
        else
            display " (file)" crlf
        end if
        incr i
    repeat
else
    display errorText crlf
end if
