# Files Example
# written in LDPL by Martín del Río
# https://www.ldpl-lang.org/
# Created for LDPL 3.0.6 'Diligent Dreadnoughtus'

# --- Description -------------------------------------
# Lists all the files in the current directory. Then
# opens a random file with .ldpl extension and displays
# its contents. If no ldpl file is found then a message
# is displayed.
# -----------------------------------------------------

DATA:
filesInDirectory is text list
numberOfFiles is number
i is number
fileContents is text

PROCEDURE:
list contents of directory "." in filesInDirectory
if errorCode is equal to 0 then
    store length of filesInDirectory in numberOfFiles
    display "Found " numberOfFiles " file(s)." crlf
    # List all the files
    while i is less than numberOfFiles do
        display "> " filesInDirectory:i crlf
        incr i
    repeat
    # Pick a random file
    store random in i
    multiply i by numberOfFiles in i
    floor i
    display "Loading " filesInDirectory:i "..." crlf
    load file filesInDirectory:i in fileContents
    store length of fileContents in i
    if i is equal to 0 then
        display "The file is empty (or maybe it is a folder!)." crlf
    else
        display fileContents
    end if
else
    display errorText crlf
end if
